// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.10;
pragma experimental ABIEncoderV2;

import {StrategyParams, VaultAPI, StrategyAPI} from "./BaseStrategy.sol";

library StrategyLib {
    function internalHarvestTrigger(
        address vault,
        address strategy,
        uint256 callCost,
        uint256 minReportDelay,
        uint256 maxReportDelay,
        uint256 debtThreshold,
        uint256 profitFactor
    ) public view returns (bool) {
        StrategyParams memory params = VaultAPI(vault).strategies(strategy);
        // Should not trigger if Strategy is not activated
        if (params.activation == 0) {
            return false;
        }

        // Should not trigger if we haven't waited long enough since previous harvest
        if ((block.timestamp - params.lastReport) < minReportDelay) return false;

        // Should trigger if hasn't been called in a while
        if ((block.timestamp - params.lastReport) >= maxReportDelay) return true;

        // If some amount is owed, pay it back
        // NOTE: Since debt is based on deposits, it makes sense to guard against large
        //       changes to the value from triggering a harvest directly through user
        //       behavior. This should ensure reasonable resistance to manipulation
        //       from user-initiated withdrawals as the outstanding debt fluctuates.
        uint256 outstanding = VaultAPI(vault).debtOutstanding();
        if (outstanding > debtThreshold) return true;

        // Check for profits and losses
        uint256 total = StrategyAPI(strategy).estimatedTotalAssets();
        // Trigger if we have a loss to report
        if (total + debtThreshold < params.totalDebt) return true;

        uint256 profit = 0;
        if (total > params.totalDebt) profit = total - params.totalDebt; // We've earned a profit!

        // Otherwise, only trigger if it "makes sense" economically (gas cost
        // is <N% of value moved)
        uint256 credit = VaultAPI(vault).creditAvailable();
        return ((profitFactor * callCost) < (credit + profit));
    }

    function internalSetRewards(
        address oldRewards,
        address newRewards,
        address vault
    ) public {
        require(newRewards != address(0));
        VaultAPI(vault).approve(oldRewards, 0);
        VaultAPI(vault).approve(newRewards, type(uint256).max);
    }
}
